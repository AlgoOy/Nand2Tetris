// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    Mux16(a=instruction, b=outalu, sel=instruction[15], out=outianda);

    Not(in=instruction[15], out=notic);
    And(a=instruction[15], b=instruction[5], out=icra);
    Or(a=notic, b=icra, out=loadra);
    ARegister(in=outianda, load=loadra, out=outregistera, out[0..14]=addressM);

    And(a=instruction[15], b=instruction[4], out=loadrd);
    DRegister(in=outalu, load=loadrd, out=outregisterd);

    And(a=instruction[15], b=instruction[3], out=writeM);

    And(a=instruction[15], b=instruction[12], out=loadinm);
    Mux16(a=outregistera, b=inM, sel=loadinm, out=outinmanda);
    And(a=instruction[15], b=instruction[11], out=zx);
    And(a=instruction[15], b=instruction[10], out=nx);
    And(a=instruction[15], b=instruction[9], out=zy);
    And(a=instruction[15], b=instruction[8], out=ny);
    And(a=instruction[15], b=instruction[7], out=f);
    And(a=instruction[15], b=instruction[6], out=no);
    ALU(x=outregisterd, y=outinmanda, zx=zx, nx=nx, zy=zy, ny=ny, f=f, no=no, out=outalu, out=outM, zr=outzr, ng=outng);
    
    And(a=instruction[2], b=outng, out=andj1);
    And(a=instruction[1], b=outzr, out=andj2);
    Or(a=outzr, b=outng, out=zrorng);
    Not(in=zrorng, out=notzrorng);
    And(a=instruction[0], b=notzrorng, out=andj3);
    Or(a=andj1, b=andj2, out=j1orj2);
    Or(a=j1orj2, b=andj3, out=j1orj2orj3);
    And(a=instruction[15], b=j1orj2orj3, out=pcload);
    PC(in=outregistera, load=pcload, inc=true, reset=reset, out[0..14]=pc);
}